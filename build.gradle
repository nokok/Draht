plugins {
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
    id 'signing'
}

group 'net.nokok.draft'
version '1.1.1'

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    implementation 'javax.inject:javax.inject:1'
    testImplementation 'com.google.inject:guice:3.0'
    testImplementation 'javax.inject:javax.inject-tck:1'
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.10'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.google.testing.compile:compile-testing:0.18'
}

task signFiles(type: Sign) {
    sign project.file('build/publications/maven/pom-default.xml')
    outputs.upToDateWhen { false }
}

signing {
    sign configurations.archives
    sign publishing.publications
}

if(!hasProperty('nexusUsername')) {
  ext.nexusUsername = "dummy"
}
if(!hasProperty('nexusPassword')) {
  ext.nexusPassword = "dummy"
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = 'draft'
            version = project.version
            from components.java
            pom {
                name = 'Draft'
                description = 'Implementation of JSR-330'
                url = 'https://github.com/nokok/Draft'
                developers {
                    developer {
                        id = 'nokok'
                        name = 'Noriyuki Kazusawa'
                        email = 'nokok.kz@gmail.com'
                    }
                }
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/nokok/Draft/blob/master/LICENSE'
                    }
                }
                scm {
                    url = 'git@github.com:nokok/Draft.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = "${nexusUsername}"
                password = "${nexusPassword}"
            }
        }
    }
}
